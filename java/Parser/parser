package Parser;

import java_cup.runtime.*;
import AST.*;
import Compiler.*;

parser code {:
	public static void main(String args[]) throws Exception {
		new parser(new Lexer.Yylex(System.in)).parse();
	}
:}

terminal IF, THEN, ELSE, END, WHILE, PLOT, DO, NOT, AND, OR, SIZE, PROG,
INT2REAL, REAL2INT, VARS, SENO, COSENO, EXPONENCIAL, PC, ASOP, MENORQUE,
MAYORQUE, MAS, DIV, POR, PAREN, TESIS, IGUALQUE, COMA, MENOS, COLOR;

terminal Integer CENT;
terminal Boolean CLOG;
terminal String IDENT, PI;
terminal Double CREAL;
terminal Type TIPO;

non terminal Prog Prog;
non terminal VDef VDef;
non terminal Vars Vars;
non terminal Decl Decl;
non terminal Size Size;
non terminal LVar LVar;
non terminal Inp Inp;
non terminal Sent Sent;
non terminal SentSimp SentSimp;
non terminal Asign Asign;
non terminal Cond Cond;
non terminal Repet Repet;
non terminal Plot Plot;
non terminal Exp Exp;

precedence left OR;
precedence left AND;
precedence left NOT;
precedence right IGUALQUE;
precedence right MAYORQUE;
precedence right MENORQUE;
precedence left MAS;
precedence left MENOS;
precedence left POR;
precedence left DIV;

start with Prog;

Prog ::= PROG IDENT:id Inp:inp Vars:vars Size:size Sent:sent {:RESULT = new Prog1(id, inp, vars, size, sent); :}; 

Inp ::= PAREN LVar:lvar TESIS {:RESULT = new Inp1(lvar); :}
		| {:RESULT = new Inp2(); :}; 

Vars ::= VARS VDef:vdef {:RESULT = new Vars1(vdef); :}
		| {:RESULT = new Vars2(); :}; 

VDef ::= Decl:decl PC {:RESULT = new VDef1(decl); :}
		| Decl:decl PC VDef:vdef {:RESULT = new VDef2(decl, vdef); :};

Decl ::= TIPO:type LVar:lvar {:RESULT = new Decl1(type, lvar); :};

LVar ::= IDENT:id {:RESULT = new LVar1(id); :}
		| IDENT:id COMA LVar:lvar {:RESULT = new LVar2(id, lvar); :};

Size ::= SIZE PAREN Exp:exp1 COMA Exp:exp2 TESIS PC {:RESULT = new Size1(exp1, exp2); :}; 

Sent ::= SentSimp:sentsimp PC Sent:sent {:RESULT = new Sent1(sentsimp, sent); :}
		| SentSimp:sentsimp PC {:RESULT = new Sent2(sentsimp); :};

SentSimp ::= Asign:asign {:RESULT = new SentSimp1(asign); :}
		| Cond:cond {:RESULT = new SentSimp2(cond); :}
		| Repet:repet {:RESULT = new SentSimp3(repet); :}
		| Plot:plot {:RESULT = new SentSimp4(plot); :};

Asign ::= IDENT:id ASOP Exp:exp {:RESULT = new Asign1(id, exp); :};

Cond ::= IF Exp:exp THEN Sent:sent END{:RESULT = new Cond1(exp, sent); :}
		|IF Exp:exp THEN Sent:sent1 ELSE Sent:sent2 END{:RESULT = new Cond2(exp, sent1, sent2); :};

Repet ::= WHILE Exp:exp DO Sent:sent END{:RESULT = new Repet1(exp, sent); :};

Plot ::= PLOT PAREN COLOR COMA Exp:exp1 COMA Exp:exp2 TESIS{:RESULT = new Plot1(exp1, exp2); :};

Exp::= CLOG:clog                        	{:RESULT=new ExpConstBool(clog); :}
      	|   Exp:e1 AND Exp:e2	            {:RESULT=new ExpAnd(e1, e2); :}
      	|   Exp:e1 OR Exp:e2	            {:RESULT=new ExpOr(e1, e2); :}
      	|   NOT Exp:e1              	    {:RESULT=new ExpNot(e1); :}
      	|   Exp:e1 IGUALQUE Exp:e2	   		{:RESULT=new ExpIgualQue(e1, e2); :}
      	|   Exp:e1 MAYORQUE Exp:e2        	{:RESULT=new ExpMayorQue(e1, e2); :}
      	|   Exp:e1 MENORQUE Exp:e2	    	{:RESULT=new ExpMenorQue(e1, e2); :}

      	|   IDENT:i1                   		{:RESULT=new ExpIdent(i1); :}
      	|   PI                   			{:RESULT=new ExpPi(); :}
      	|   PAREN Exp:e1 TESIS           	{:RESULT=new ExpParent(e1); :}

      	|   CENT:n                        	{:RESULT=new ExpConsInt(n); :}
		|   CREAL:creal                     {:RESULT=new ExpConstReal(creal); :}
      	|   Exp:e1 MAS Exp:e2	            {:RESULT=new ExpMas(e1, e2); :}
      	|   Exp:e1 MENOS Exp:e2           	{:RESULT=new ExpMenos(e1, e2); :}
      	|   Exp:e1 POR Exp:e2  	        	{:RESULT=new ExpPor(e1, e2); :}
      	|   Exp:e1 DIV Exp:e2	            {:RESULT=new ExpDiv(e1, e2); :}
      	|   MENOS Exp:e1              		{:RESULT=new ExpOpuesto(e1); :}
		|	REAL2INT PAREN Exp:exp TESIS 	{:RESULT = new ExpReal2Int(exp); :}
		| 	INT2REAL PAREN Exp:exp TESIS 	{:RESULT = new ExpInt2Real(exp); :}
		| 	SENO PAREN Exp:exp TESIS 		{:RESULT = new ExpSeno(exp); :}
		| 	COSENO PAREN Exp:exp TESIS 		{:RESULT = new ExpCoseno(exp); :}
		| 	EXPONENCIAL PAREN Exp:exp TESIS {:RESULT = new Exponencial(exp); :};

